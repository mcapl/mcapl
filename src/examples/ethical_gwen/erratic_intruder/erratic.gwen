// ----------------------------------------------------------------------------
// Copyright (C) 2016 Louise A. Dennis, Michael Fisher, Marija Slavkovik and Matt Webster
// 
// This file is part of Ethical Gwendolen
//
// Ethical Gwendolen is free software; you can redistribute it and/or
// modify it under the terms of the GNU Lesser General Public
// License as published by the Free Software Foundation; either
// version 3 of the License, or (at your option) any later version.
// 
// Ethical Gwendolen is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
// Lesser General Public License for more details.
// 
// You should have received a copy of the GNU Lesser General Public
// License along with Gwendolen if not, write to the Free Software
// Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
// 
// To contact the authors:
// http://www.csc.liv.ac.uk/~lad
//----------------------------------------------------------------------------
:name: executive

:Ethical Policy:

E(flightPhase(emergAvoid), doNotViolateRoADetectAvoidTurnRight, 2)
E(flightPhase(emergAvoid), doNotViolateRoA500Feet, 2)
E(flightPhase(emergAvoid), doNotCollideObjectsOnGround, 3)
E(flightPhase(emergAvoid), doNotCollideAircraft, 4)

:Initial Beliefs:

flightPhase(cruise)

:Reasoning Rules:

avoid_collision :- ~ das(intruder, headOn);


other_choices_violated(T) :- ~ untried_plan_not_violates(T);
an_ethic_in(P, [Eth|T]) :- ethics_of(P, Eth) [ethics];
an_ethic_in(P, [Eth|T]) :- an_ethic_in(P, T);
un_tried_plan :- applicable(P) [applicable_plans], ~ already_tried(P);
untried_plan(P) :- applicable(P) [applicable_plans], ~ already_tried(P);
some_applicable :- applicable(P) [applicable_plans];
some_plan_tried :- already_tried(P);
untried_plan_not_violates(T) :- untried_plan(P), ~ an_ethic_in(P, T);

:Initial Goals:

startup [perform]

:Plans:

+!startup [perform]: {True} <- +! missionComplete [achieve];

// DAS (detect and avoid sensor) is polled
+!missionComplete [achieve] : {B flightPhase(cruise) } <-
 *missionComplete;
+!missionComplete [achieve] : {B flightPhase(emergAvoid) } <-
 *missionComplete;
+!avoid_collision [achieve] : {B flightPhase(emergAvoid), ~B route(emergAvoid, Route)} <-
   plan(reqEmergRoute,turnRight), 
   *route(emergAvoid, R),
   enactRoute(R),
   wait;
   
 +das(intruder, headOn) : {B flightPhase(cruise)} <-
  +.lock,
  -flightPhase(cruise), 
  +flightPhase(emergAvoid), 
  -.lock,
  +! avoid_collision [achieve];
  
-das(intruder, headOn) : {B flightPhase(emergAvoid)} <-
  +.lock,
  -flightPhase(emergAvoid),
  +flightPhase(cruise),
  -.lock;