GWENDOLEN

:name: jackal

:Initial Beliefs:

location_coordinate(0,door,-8.18,-3.16,0.0)
location_coordinate(1,bigtankfront,-4.07,-7.24,0.0)
location_coordinate(2,bigtankside,-7.25,-5.26,0.0)
location_coordinate(3,tank1top,-0.49,-3.51,0.0)
location_coordinate(4,tank1right,2.30,-1.93,0.0)
location_coordinate(5,tank1bottom,5.48,-3.48,0.0)
location_coordinate(6,tank2top,-0.47,-6.52,0.0)
location_coordinate(7,tank2left,1.98,-7.88,0.0)
location_coordinate(8,tank2bottom,5.36,-6.31,0.0)
location_coordinate(9,tankset,6.80,-5.69,0.0)
location_coordinate(10,pipes,7.79,-2.67,0.0)
location_coordinate(11,dangerroom,1.30,2.20,0.0)
location_coordinate(12,stairs,2.94,8.56,0.0)

next_location(1,2)
next_location(2,3)
next_location(3,4)
next_location(4,5)
next_location(5,0)
next_location(6,7)
next_location(7,8)
next_location(8,9)
next_location(9,10)
next_location(10,11)
next_location(11,12)
next_location(12,0)

:Reasoning Rules:

safe :- ~danger_red, ~danger_orange, ~going(0);

:Initial Goals:

inspect(1) [perform]

:Plans:

+!inspect(Location) [perform] : { ~B danger_red, ~B danger_orange, ~B going(0), B location_coordinate(Location,LocationName,X,Y,Z) } 
	<- +going(Location), move(X,Y,Z);
+!inspect(Location) [perform] : { B danger_red } 
	<- *safe,
	+!inspect(Location) [perform];
+!inspect(Location) [perform] : { B danger_orange } 
	<- *safe,
	+!inspect(Location) [perform];
+!inspect(Location) [perform] : { B going(0) } 
	<- *safe,
	+!inspect(Location) [perform];
	
+movebase_result(Id,3) : { B going(L1), B next_location(L1, L2) } <- 
						-going(L1), 
						print("Movement to location "),
						print(L1),
						print(" ended with success."),
						inspect,
						+inspected(L1), // Note addition from original, delete if necessary
						+!inspect(L2) [perform];
+movebase_result(Id,3) : { B going(0) } <-print("Wait for decontamination to proceed."), do_nothing;
+movebase_result(Id,3) : { ~B going(L) } <- -movebase_result(Id, 3);
+movebase_result(Id,2) : { B going(Location) } <- print("Movement to location ",Location," ended with failure.");

+danger_red : { ~B going(0), B location_coordinate(0,door,X,Y,Z) } <- print("Robot is in red radiation zone move to the door for decontamination procedures."), +going(0), update_door, move(X,Y,Z);
+danger_orange : { ~B going(0), B location_coordinate(0,door,X,Y,Z) } <- print("Robot is in orange radiation zone move to the door for decontamination procedures."), +going(0), update_door, move(X,Y,Z); 
