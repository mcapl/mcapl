GWENDOLEN

:name: provider
:Initial Beliefs:
data(full_name)
data(dob)
data(gender)
data(bmi)
data(contact)
data(home_address)
data(marital_status)
data(occupation)
data(education)
data(gps)
data(social_media)
:Initial Goals:
data_requests_sent [achieve]
:Plans:
+!data_requests_sent [achieve] : {B data(D), ~B data_sent(D)} <-
    .send(user, :tell, belief(data(D))),
    +data_sent(D);
+!data_requests_sent [achieve] : {True} <-
    .send(user, :tell, all_data_requests_sent),
    +data_requests_sent;

//////////////////////////////////////////////////////////////////////////////
:name: user
:Initial Beliefs:
belief(workout_plan(present))
belief(diet_recommendations(present))
belief(daily_physical_activities(monitored))
:Initial Goals:
:Plans:
+.received(:tell, B) : {True} <- +B;
+all_data_requests_sent : {True} <- +!beliefs_shared [achieve];
+!beliefs_shared [achieve] : {B belief(X), ~B belief_sent(X)} <-
    .send(agent, :tell, X),
    +belief_sent(X);
+!beliefs_shared [achieve] : {True} <-
    .send(agent, :tell, all_beliefs_shared),
    +beliefs_shared;


//////////////////////////////////////////////////////////////////////////////
:name: agent
:Initial Beliefs:
:Initial Goals:
:Plans:
+.received(:tell, B) : {True} <- +B;

+data(X) : {True} <-
        *all_beliefs_shared,
        +!data_requirement(X) [perform];

+!data_requirement(marital_status) [perform] : {True} <- .send(user, :perform, decline(marital_status)), +decline(marital_status);
+!data_requirement(occupation) [perform] : {True} <- .send(user, :perform, decline(occupation)), +decline(occupation);
+!data_requirement(education) [perform] : {True} <- .send(user, :perform, decline(education)), +decline(education);

+!data_requirement(dob) [perform] : {True} <- .send(user, :perform, substitute(dob, age)), +substitute(dob, age);

+!data_requirement(gps) [perform] : {B data(home_address)} <- .send(user, :perform, decline(gps)), +decline(gps);

+!data_requirement(bmi) [perform] : {B data(dob), B data(gender), B workout_plan(present), B diet_recommendations(present), B daily_physical_activities(monitored)} <- .send(user, :perform, decline(bmi)), +decline(bmi);

+!data_requirement(X) [perform] : {True} <- .send(user, :perform, approve(X)), +approve(X);